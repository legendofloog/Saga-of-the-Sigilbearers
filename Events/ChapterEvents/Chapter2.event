EventPointerTable(Chapter2Events,PointerList) //7 is the ID for prologue events in vanilla FE8

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(10,SilverBowSnipersWarning,1)
TurnEventPlayer(7,RenzerSpawn,8)
TurnEventPlayer(14,RenzerSpawnWarning,7)
END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(9,EindrideRecruit,Asgeir,Eindride)
//CharacterEventBothWays(10,RuneRagna,Rune,Ragna)
//CharacterEventBothWays(11,BrynjaEimund,Brynja,Eimund)
//CharacterEventBothWays(12,AsgeirRagna,Asgeir,Ragna)
END_MAIN

LocationBasedEvents:
Escape(16,22)
Escape(17,22)
Escape(18,22)
Village(13,HoltornTemple,18,14)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
AREA 8 EindrideRides [4,12] [11,21]
AFEV 0x0 CheckNoUnits 0x0
END_MAIN 

Dunno1:
END_MAIN 

Dunno2:
END_MAIN 

Dunno3:
END_MAIN 

Tutorial:
END_MAIN

Units1:

UNIT Rune Outcast Rune Level(1,Ally,False) [1,2] 0x0 0x0 0x0 0x1 [IronAxe,HandAxe,Vulnerary] NoAI
UNIT Asgeir AsgeirLord Rune Level(1,Ally,False) [3,2] 0x0 0x0 0x0 0x1 [Glaive,IronLance,Vulnerary] NoAI
UNIT Ragna RagnaLord Rune Level(1,Ally,False) [2,3] 0x0 0x0 0x0 0x1 [Thunder,Flux,Vulnerary] NoAI
UNIT Eimund Warrior Rune Level(1,Ally,False) [3,3] 0x0 0x0 0x0 0x1 [BraveAxe,Thunder,Vulnerary] NoAI
UNIT Ulfr Fighter Rune Level(1,Ally,False) [1,3] 0x0 0x0 0x0 0x1 [IronAxe,Vulnerary] NoAI
UNIT Ivarr Hunter Rune Level(1,Ally,False) [1,4] 0x0 0x0 0x0 0x1 [IronBow,Vulnerary] NoAI
UNIT Brynja SwordKnight Rune Level(9,Ally,False) [2,4] 0x0 0x0 0x0 0x1 [SilverSword,IronSword,Vulnerary] NoAI
UNIT Eimund Myrmidon_F Rune Level(1, Ally, False) [3,4] 0x00 0x00 0x00 0x01 [Shamshir,IronSword,Vulnerary] NoAI
UNIT

KleosUnitGroup:
UNIT Kleos Troubadour Rune Level(1,Ally,False) [18,15] 0x0 0x0 0x0 0x1 [Heal,IronSword,Vulnerary] NoAI
UNIT

EnemyUnits:
UNIT 0x70 Fighter Renzer Level(2, Enemy, True) [2,8] 0x00 0x00 0x0 0x00 [IronAxe] PursueWithoutHeedAI
UNIT 0x70 Myrmidon_F Renzer Level(2, Enemy, True) [6,6] 0x00 0x00 0x0 0x00 [IronSword] NeverMoveAI
UNIT 0x70 SwordKnight Renzer Level(2, Enemy, True) [4,13] 0x00 0x00 0x0 0x00 [SteelSword] NeverMoveAI
UNIT 0x70 LanceKnight Renzer Level(2, Enemy, True) [5,13] 0x00 0x00 0x0 0x00 [SteelLance] NeverMoveAI
UNIT 0x70 SwordKnight Renzer Level(2, Enemy, True) [11,11] 0x00 0x00 0x0 0x00 [SteelSword] NeverMoveAI
UNIT 0x70 AxeKnight Renzer Level(2, Enemy, True) [12,11] 0x00 0x00 0x0 0x00 [SteelAxe] NeverMoveAI
UNIT 0x70 Mercenary Renzer Level(2, Enemy, True) [5,10] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
UNIT 0x70 Fighter Renzer Level(2, Enemy, True) [1,17] 0x00 0x00 0x0 0x00 [IronAxe] AttackInTwoMovementsAI
UNIT 0x70 Mercenary Renzer Level(2, Enemy, True) [5,21] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
UNIT 0x70 LanceCav Renzer Level(2, Enemy, True) [11,15] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Fighter Renzer Level(2, Enemy, True) [15,12] 0x00 0x00 0x0 0x00 [IronAxe] AttackInTwoMovementsAI
UNIT 0x70 Soldier Renzer Level(2, Enemy, True) [14,17] 0x00 0x00 0x0 0x00 [IronGreatlance] AttackInRangeAI
UNIT 0x70 Soldier Renzer Level(2, Enemy, True) [8,9] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Soldier Renzer Level(2, Enemy, True) [7,20] 0x00 0x00 0x0 0x00 [IronLance,Vulnerary] AttackInRangeAI
UNIT 0x70 Hunter Renzer Level(2, Enemy, True) [12,18] 0x00 0x00 0x0 0x00 [SteelBow] AttackInRangeAI
UNIT 0x70 Hunter Renzer Level(2, Enemy, True) [1,12] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
UNIT 0x70 Axe_Dragonrider Renzer Level(2, Enemy, True) [10,17] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Mercenary Renzer Level(5, Enemy, True) [12,21] 0x00 0x00 0x0 0x00 [Zanbato] AttackInRangeAI
UNIT 0x70 AxeCav Renzer Level(2,Enemy,True) [19,21] 0x00 0x00 0x0 0x00 [IronAxe] AttackInTwoMovementsAI
UNIT Eindride LanceCav Renzer Level(3, Ally, False) [3,20] 0x00 0x00 0x0 0x00 [IronLance, Vulnerary] NeverMoveAI
UNIT 0x70 Hunter Renzer Level(2, Enemy, True) [0,22] 0x00 0x00 0x0 0x00 [IronBow] AttackInTwoMovementsAI
UNIT 0x70 Hunter Renzer Level(2, Enemy, True) [18,20] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
UNIT 0x70 SwordKnight Renzer Level(2, Enemy, True) [16,22] 0x00 0x00 0x0 0x00 [SteelSword] AttackInRangeAI
UNIT 0x70 AxeKnight Renzer Level(2, Enemy, True) [17,22] 0x00 0x00 0x0 0x00 [SteelAxe] AttackInRangeAI
UNIT 0x70 LanceKnight Renzer Level(2, Enemy, True) [18,22] 0x00 0x00 0x0 0x00 [SteelLance] AttackInRangeAI
UNIT 0x70 Axe_Dragonrider Renzer Level(2, Enemy, True) [1,20] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT

RenzerUnitGroup:
UNIT Renzer Warrior 0x00 Level(2, Enemy, False) [18,1] 0x00 0x00 0x0 0x00 [SilverBow, KillerAxe] NeverMoveAI
UNIT

SilverBowSnipersUnitGroup:
UNIT 0x71 Sniper Renzer Level(1, Enemy, True) [17,0] 0x00 0x00 0x0 0x00 [SilverBow] NeverMoveAI
UNIT 0x71 Sniper Renzer Level(1, Enemy, True) [19,0] 0x00 0x00 0x0 0x00 [SilverBow] NeverMoveAI
UNIT

Traps1:
BlueSigil(16,22)
BlueSigil(17,22)
BlueSigil(18,22)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

ALIGN 4
EindrideAi2: 
	AiTryTalkToCharacter(Asgeir)
	InjectAi2(Ch2_EindrideAI2, EindrideAi2)
ALIGN 4

BeginningScene:
MUSC RippleIslandEnding
SPAWN_ALLY Rune [19,1]
SPAWN_ALLY Ragna [17,0]
SPAWN_ALLY Eimund [18,2]
SPAWN_ALLY Brynja [17,1]
SPAWN_ALLY Asgeir [19,0]
ENUN
CAMERA [19,0]
STAL 60
SetBackground(0x18)
Text(Ch2Scene1)
CALL $9EE2C4
STAL 10
MOVE 10 Eimund [11, 13]
STAL 30
MOVE 10 Brynja [12,13]
STAL 30
MOVE 10 Rune [10,14]
STAL 30
CAMERA [19,13]
MOVE 10 Ragna [11,15]
STAL 30
MOVE 10 Asgeir [11,14]
ENUN
CAMERA Eimund
MOVE 10 Eimund [5, 7]
STAL 30
MOVE 10 Brynja [5,9]
STAL 30
MOVE 10 Rune [6,8]
STAL 30
CAMERA Eimund
MOVE 10 Ragna [7,8]
STAL 30
MOVE 10 Asgeir [6,9]
ENUN
CAMERA Eimund
MOVE 10 Eimund [1, 2]
STAL 30
MOVE 10 Brynja [4,3]
STAL 30
MOVE 10 Rune [2,2]
STAL 30
CAMERA Eimund
MOVE 10 Ragna [3,1]
STAL 30
MOVE 10 Asgeir [3,3]
ENUN
CAMERA Eimund
MUSC Rovers
EVBIT_T 5
TEXTSTART
TEXTSHOW Ch2Scene2
TEXTEND
MOVE 10 Ragna [3,0]
ENUN
HIDEUNIT Ragna
TEXTCONT
TEXTEND
MOVE 10 Rune [2,0]
ENUN
HIDEUNIT Rune
TEXTCONT
TEXTEND
MOVE 10 Brynja [12,10]
ENUN
TEXTCONT
TEXTEND
LOAD1 1 SilverBowSnipersUnitGroup
ENUN
CAMERA [19,2]
CURSOR_FLASHING [17, 0]
STAL 30
REMOVE_CURSORS
CURSOR_FLASHING [19, 0]
STAL 30
REMOVE_CURSORS
CAMERA Brynja
MUSC 0x7FFF
STAL 30
MUSC Trisection
TEXTCONT
TEXTEND
CamOn
MOVE 10 Brynja [4,3]
ENUN
CamOff
TEXTCONT
TEXTEND
SHOWUNIT Rune
MOVE 10 Rune [2,2]
ENUN
SHOWUNIT Ragna
MOVE 10 Ragna [3,1]
ENUN
TEXTCONT
TEXTEND
REMA
EVBIT_F 5
MUSC 0x7FFF
CAMERA [19,0]
FADI 5
LOAD1 1 EnemyUnits
ENUN
FADU 5
CUSE Eindride
GotoPrepScreen
ENDA

SilverBowSnipersWarning:
MUSC AC_KK_Condor
CHECK_ALIVE Ivarr
BEQ 4 s0 sC
	CAMERA Ivarr
	CURSOR_FLASHING Ivarr
	STAL 60
	REMOVE_CURSORS
	Text(Ch2IvarrAsgeirTalk)
LABEL 4
CAMERA Eindride
CURSOR_FLASHING Eindride
STAL 60
REMOVE_CURSORS
Text(EindrideIntroText)
CAMERA [19,0]
CURSOR_FLASHING [17,0]
STAL 30
REMOVE_CURSORS
CURSOR_FLASHING [19,0]
STAL 30
REMOVE_CURSORS
Text(Ch2SilverBowSnipersPreBattleText)
NoFade
ENDA


HoltornTemple:
MUSC Rovers
CHECK_ACTIVE 
SVAL s1 Ragna
BNE 12 s1 sC
	SetBackground(Temple)
	Text(Ch2TempleRagnaText)
	CALL $9EE2C4
	SVAL s3 SpiritDust
	GIVEITEM Ragna
	GOTO 14
LABEL 12
SVAL s1 Rune
BNE 13 s1 sC
	SetBackground(Temple)
	Text(Ch2TempleRuneText)
	CALL $9EE2C4
	SVAL s3 SpiritDust
	GIVEITEM Rune
	GOTO 14
LABEL 13
SetBackground(Temple)
Text(Ch2TempleGenericText)
CALL $9EE2C4
LABEL 14
LOAD1 1 KleosUnitGroup
NoFade
ENDA

RenzerSpawnWarning:
CAMERA [19,0]
Text(RenzerSpawnWarningText)
NoFade
ENDA

RenzerSpawn:
LOAD1 1 RenzerUnitGroup
ENUN
CAMERA Renzer
Text(RenzerSpawnText)
SVAL s1 0x00000000
CHAI Renzer
CHAI 0x71
NoFade
ENDA

EindrideRides:
CHECK_ACTIVE
SVAL s1 Asgeir
BNE 2 s1 sC
	CAMERA Eindride
	Text(Ch2EindrideRidesText)
	SVAL s1 0x00000B06
	CHAI Eindride
	GOTO 3
LABEL 2
ENUF 8
LABEL 3
NoFade
ENDA

EindrideRecruit:
MUSC FE11_TogetherWeRide
Text(AsgeirRecruitsEindrideText)
SVAL s1 0x00000000
REVEAL Eindride
CUSA Eindride
NoFade
ENDA

CheckNoUnits:
ASMC GetBlueUnitCountASMC
BNE 1 s0 sC
	ENUT 0x03
	CALL EndingScene
LABEL 1
NoFade
ENDA

EndingScene:
MUSC Rovers
SetBackground(0x18)
Text(Ch2SceneEnding)
MNC2 0x02 //goto chapter 3
NoFade
ENDA
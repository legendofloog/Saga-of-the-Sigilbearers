EventPointerTable(Chapter3Events,PointerList) //7 is the ID for prologue events in vanilla FE8

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventNPC(7,FridaRollsOut,2)
TurnEventPlayer(8,HildaCharges,7)
TurnEventPlayer(9,WyvernCharge,5)
TurnEventPlayer(19,Turn1Stuff,1)
TurnEventPlayer(21,WyvernChargeWarning,4)
END_MAIN

ALIGN 32
CharacterBasedEvents:
CharacterEvent(10,FridaSigneRecruit,Signe,Frida)
CharacterEvent(11,FridaEindrideRecruit,Eindride,Frida)
CharacterEvent(12,FridaAsgeirRecruit,Asgeir,Frida)
CharacterEventBothWays(20,SigneEindrideTalk,Signe,Eindride)
END_MAIN

LocationBasedEvents:
Village(13,ElderVisit,15,9)
Village(14,WestVillage,2,1)
Village(15,FirstVillage,2,7)
Village(16,SecondVillage,8,8)
Village(17,ThirdVillage,11,15)
Village(18,FourthVillage,14,17)
END_MAIN

MiscBasedEvents:
CauseGameOverIfLordDies
DefeatBoss(EndingScene)
END_MAIN

GameOverStart:
ASMC 0x800D391
NoFade
ENDA

Dunno1:
END_MAIN 

Dunno2:
END_MAIN 

Dunno3:
END_MAIN 

Tutorial:
END_MAIN

Units1:
UNIT Rune Outcast 0x00 Level(2,Ally,False) [23,2] 0x0 0x0 0x0 0x1 [IronAxe,HandAxe,Vulnerary] NoAI
UNIT Asgeir AsgeirLord Rune Level(1,Ally,False) [25,6] 0x0 0x0 0x0 0x1 [Glaive,IronLance,Vulnerary] NoAI
UNIT Ragna RagnaLord Rune Level(1,Ally,False) [25,3] 0x0 0x0 0x0 0x1 [Thunder,Flux,Vulnerary] NoAI
UNIT Eimund Warrior Rune Level(1,Ally,False) [22,3] 0x0 0x0 0x0 0x1 [BraveAxe,Thunder,Vulnerary] NoAI
UNIT Ulfr Fighter Rune Level(2,Ally,False) [26,5] 0x0 0x0 0x0 0x1 [IronAxe,Vulnerary] NoAI
UNIT Ivarr Hunter Rune Level(2,Ally,False) [24,5] 0x0 0x0 0x0 0x1 [IronBow,Vulnerary] NoAI
UNIT Brynja SwordKnight Rune Level(9,Ally,False) [24,4] 0x0 0x0 0x0 0x1 [SilverSword,IronSword,Vulnerary] NoAI
UNIT Eimund Myrmidon_F Rune Level(2, Ally, False) [24,2] 0x00 0x00 0x00 0x01 [Shamshir,IronSword,Vulnerary] NoAI
UNIT Eimund LanceCav Rune Level(2, Ally, False) [26,4] 0x00 0x00 0x0 0x01 [IronLance, Vulnerary] NoAI
UNIT Signe Soldier_F Rune Level(2,Ally,False) [23,4] 0x00 0x00 0x0 0x01 [IronLance,Vulnerary] NoAI
UNIT

EnemyUnits:

UNIT Hilda Axe_Dragonrider 0x00 Level(5,Enemy,False) [0,16] 0x00 0x00 0x0 0x00 [HandAxe, IronAxe] NeverMoveAI
UNIT 0x73 Axe_Dragonrider Hilda Level(4, Enemy, True) [1,17] 0x00 0x00 0x0 0x00 [IronAxe] MoveWithLeaderAI
UNIT 0x73 Axe_Dragonrider Hilda Level(4, Enemy, True) [1,15] 0x00 0x00 0x0 0x00 [IronAxe] MoveWithLeaderAI
UNIT 0x73 Axe_Dragonrider Hilda Level(4, Enemy, True) [2,16] 0x00 0x00 0x0 0x00 [IronAxe] MoveWithLeaderAI
UNIT 0x71 Axe_Dragonrider Hilda Level(4, Enemy, True) [0,14] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x71 Axe_Dragonrider Hilda Level(4, Enemy, True) [3,17] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [19,2] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Soldier Hilda Level(3,Enemy,True) [18,3] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Soldier Hilda Level(3, Enemy, True) [14,2] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Mercenary Hilda Level(3, Enemy, True) [10,1] 0x00 0x00 0x0 0x00 [SteelSword] AttackInTwoMovementsAI
UNIT 0x70 Myrmidon Hilda Level(3, Enemy, True) [17,8] 0x00 0x00 0x0 0x00 [IronSword,Vulnerary] AttackInTwoMovementsAI
UNIT 0x70 Myrmidon Hilda Level(3, Enemy, True) [6,13] 0x00 0x00 0x0 0x00 [IronSword] AttackInTwoMovementsAI
UNIT 0x70 Mercenary Hilda Level(3, Enemy, True) [23,9] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [26,9] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Soldier Hilda Level(3, Enemy, True) [24,12] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Archer Hilda Level(3, Enemy, True) [19,9] 0x00 0x00 0x0 0x00 [IronBow] AttackInRangeAI
UNIT 0x70 AxeKnight Hilda Level(3,Enemy,True) [18,14] 0x00 0x00 0x0 0x00 [SteelAxe] NeverMoveAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [17,19] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [19,17] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 SwordCav Hilda Level(3, Enemy, True) [8,0] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [6,4] 0x00 0x00 0x0 0x00 [IronAxe] AttackInTwoMovementsAI
UNIT 0x70 LanceCav Hilda Level(3, Enemy, True) [3,8] 0x00 0x00 0x0 0x00 [IronLance] AttackInRangeAI
UNIT 0x70 Hunter Hilda Level(3, Enemy, True) [10,7] 0x00 0x00 0x0 0x00 [IronLongbow] AttackInTwoMovementsAI
UNIT 0x70 Myrmidon_F Hilda Level(3, Enemy, True) [8,5] 0x00 0x00 0x0 0x00 [IronSword] AttackInRangeAI
UNIT 0x70 Mercenary Hilda Level(3, Enemy, True) [4,6] 0x00 0x00 0x0 0x00 [SteelSword] AttackInTwoMovementsAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [5,18] 0x00 0x00 0x0 0x00 [IronAxe,Talisman] AttackInRangeAI
UNIT 0x70 Soldier Hilda Level(3, Enemy, True) [0,16] 0x00 0x00 0x0 0x00 [IronLance,Vulnerary] AttackInRangeAI
UNIT 0x70 Fighter Hilda Level(3, Enemy, True) [16,11] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 AxeCav Hilda Level(3, Enemy, True) [13,12] 0x00 0x00 0x0 0x00 [IronAxe] AttackInRangeAI
UNIT 0x70 Lancer Hilda Level(3, Enemy, True) [9,9] 0x00 0x00 0x0 0x00 [IronLance] AttackInTwoMovementsAI
UNIT

FridaGang:
UNIT Frida Mercenary_F 0x00 Level(5,NPC,False) [21,18] 0x00 0x00 0x0 0x00 [IronBlade, Vulnerary] PursueWithoutHeedAI
UNIT 0x70 Fighter Frida Level(3,NPC,True) [22,17] 0x00 0x00 0x0 0x00 [IronAxe] MoveWithLeaderAI
UNIT 0x70 Fighter Frida Level(3,NPC,True) [22,19] 0x00 0x00 0x0 0x00 [IronAxe] MoveWithLeaderAI
UNIT

SolveigTroops:
UNIT 0x71 Axe_Dragonrider Solveig Level(4, NPC, True) [0,15] 0x00 0x00 0x0 0x00 [IronAxe] NoAI
UNIT 0x70 Axe_Dragonrider Solveig Level(4, NPC, True) [0,17] 0x00 0x00 0x0 0x00 [IronAxe] NoAI
UNIT

ElderUnitGroup:
UNIT 0x70 Civilian_2 0x00 Level(4, NPC, True) [15,9] 0x00 0x00 0x0 0x00 [0x0] NoAI
UNIT

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
SetBackground(0x1C)
Text(Ch3Scene1)
SetBackground(0x03)
MUSC OneHundredOJ_KaiTheme
EVBIT_T 5
TEXTSTART
TEXTSHOW Ch3Scene2
TEXTEND
MUSC Sortie_Formation
TEXTCONT
TEXTEND
REMA
CALL $9EE2C4
FADI 50
LOAD1 1 EnemyUnits
ENUN
FADU 5
SPAWN_ALLY Rune [25,1]
SPAWN_ALLY Asgeir [26,2]
CAMERA Rune
STAL 30
TEXTSTART
TEXTSHOW Ch3Scene3
TEXTEND
SPAWN_NPC Signe [25,13]
MOVE 15 Signe [25,2]
ENUN
TEXTCONT
TEXTEND
REMA
CURSOR_FLASHING [25, 2]
STAL 60
REMOVE_CURSORS
CUSA Signe
STAL 30
EVBIT_F 5
CAMERA Hilda
Text(HildaOpeningText)
REMOVEUNIT Signe
LOAD1 0 Units1
ENUN
PREP
ENDA

Turn1Stuff:
MUSC FE11_Preparations
CAMERA [15,9]
CURSOR_FLASHING [15,9]
STAL 60
REMOVE_CURSORS
SetBackground(House2)
Text(ElderIntroText)
CALL $9EE2C4
CAMERA [11,15]
CURSOR_FLASHING [11,15]
STAL 60
REMOVE_CURSORS
SetBackground(House1)
Text(VillagersIntroText)
CALL $9EE2C4
NoFade
ENDA

SigneEindrideTalk:
MUSC Rovers
Text(EindrideSigneTalkText)
NoFade
ENDA

FridaRollsOut:
CAMERA [25,18]
MUSC AC_KK_Condor
SetBackground(0x01)
Text(FridaSpawnText)
CALL $9EE2C4
LOAD1 1 FridaGang
ENUN
TILECHANGE 0x0D
NoFade
ENDA

HildaCharges:
CAMERA Hilda
Text(HildaChargeText)
SVAL s1 0x00040000
CHAI Hilda
NoFade
ENDA

WyvernChargeWarning:
CAMERA Hilda
Text(WyvernChargeWarningText)
NoFade
ENDA

WyvernCharge:
CAMERA Hilda
Text(HildaSoldiersText)
SVAL s1 0x00040400
CHAI 0x71
NoFade
ENDA

FridaSigneRecruit:
MUSC FE11_TogetherWeRide
Text(FridaSigneRecruitText)
CUSA Frida
ENUT 11
ENUT 12
NoFade
ENDA

FridaEindrideRecruit:
MUSC FE11_TogetherWeRide
Text(FridaEindrideRecruitText)
CUSA Frida
ENUT 10
ENUT 12
NoFade
ENDA

FridaAsgeirRecruit:
MUSC FE11_TogetherWeRide
Text(AsgeirFridaRecruitText)
CUSA Frida
ENUT 10
ENUT 11
NoFade
ENDA

ElderVisit:
	CHECK_ACTIVE 
	SVAL s1 Signe
	BNE 1 s1 sC
		MUSC AC_KK_Condor
		VillageEventItem(Ch3ElderHouseSigneText, House2, Javelin)
	LABEL 1
	SVAL s1 Frida
	BNE 2 s1 sC
		MUSC AC_KK_Condor
		VillageEventItem(Ch3ElderHouseFridaText, House2, SteelBlade)
	LABEL 2
	SVAL s1 Eindride
	BNE 3 s1 sC
		MUSC FE11_Preparations
		VillageEventItem(Ch3ElderHouseEindrideText, House2, Javelin)
	LABEL 3
		MUSC AC_KK_Condor
		VillageEventItem(Ch3ElderHouseGenericText, House2, Vulnerary)

WestVillage:
	VillageEventItem(Ch3SteelBowHouseText, House1, SteelBow)

FirstVillage:
SetBackground(House1)
Text(VillageText)
CALL $9EE2C4
COUNTER_INC 0
NoFade
ENDA

SecondVillage:
SetBackground(House1)
Text(VillageText)
CALL $9EE2C4
COUNTER_INC 0
NoFade
ENDA

ThirdVillage:
SetBackground(House1)
Text(VillageText)
CALL $9EE2C4
COUNTER_INC 0
NoFade
ENDA

FourthVillage:
SetBackground(House1)
Text(VillageText)
CALL $9EE2C4
COUNTER_INC 0
NoFade
ENDA

EndingScene:
EVBIT_T 5
MUSC 0x7FFF
FADI 15
REMOVEALL_BLUE
REMOVEALL_RED
REMOVEALL_GREEN
CHECK_ALIVE Signe
BEQ 5 s0 sC
	CHECK_ALIVE Frida
	BEQ 6 s0 sC
		TILEREVERT 7
		SPAWN_ALLY Frida [14,11]
		SPAWN_ALLY Signe [16,11]
		ENUN
		CheckMaxHP(Frida)
		SADD s1 sC s0
		SET_HP Frida
		CheckMaxHP(Signe)
		SADD s1 sC s0
		SET_HP Signe
		CAMERA_CENTERED [15,11]
		FADU 5
		MUSC FE11_Preparations
		TEXTSTART
		TEXTSHOW Ch3EndingScene1AllAlive
		TEXTEND
		SPAWN_ALLY Asgeir [4,11]
		ENUN
		CheckMaxHP(Asgeir)
		SADD s1 sC s0
		SET_HP Asgeir
		MOVE 15 Asgeir [15,12]
		ENUN
		TEXTCONT
		TEXTEND
		LOAD1 1 ElderUnitGroup
		ENUN
		CAMERA_CENTERED 0x70
		CURSOR_FLASHING 0x70
		STAL 60
		REMOVE_CURSORS
		TEXTCONT
		TEXTEND
		REMA
		GOTO 7
	LABEL 6
	SPAWN_ALLY Signe [15,11]
	ENUN
	CheckMaxHP(Signe)
	SADD s1 sC s0
	SET_HP Signe
	CAMERA_CENTERED Signe
	FADU 5
	MUSC FE11_Preparations
	Text(Ch3EndingScene1SigneAlive)
	GOTO 7
LABEL 5
CHECK_ALIVE Frida
BEQ 7 s0 sC
	SPAWN_ALLY Frida [15,11]
	ENUN
	CheckMaxHP(Frida)
	SADD s1 sC s0
	SET_HP Frida
	CAMERA_CENTERED Frida
	FADU 5
	MUSC FE11_Preparations
	Text(Ch3EndingScene1FridaAlive)
LABEL 7
COUNTER_CHECK 0
SVAL s1 4
BNE 4 s1 sC
	Text(Ch3EndingScene1RedGemGet)
	SVAL s3 RedGem
	GIVEITEM Asgeir
LABEL 4
MUSC 0x7FFF
FADI 15
REMOVEALL_BLUE
REMOVEALL_GREEN
SPAWN_ALLY Rune [11,11]
SPAWN_ALLY Asgeir [13,11]
ENUN
CheckMaxHP(Rune)
SADD s1 sC s0
SET_HP Rune
CheckMaxHP(Asgeir)
SADD s1 sC s0
SET_HP Asgeir
CAMERA_CENTERED [12,11]
FADU 5
TEXTSTART
TEXTSHOW Ch3EndingScene2
TEXTEND
SPAWN_NPC Solveig [1,16]
LOAD1 1 SolveigTroops
ENUN
CAMERA Solveig
CURSOR_FLASHING Solveig
STAL 60
REMOVE_CURSORS
CURSOR_FLASHING [0,15]
STAL 60
REMOVE_CURSORS
CURSOR_FLASHING [0,17]
STAL 60
REMOVE_CURSORS
CAMERA_CENTERED Asgeir
TEXTCONT
TEXTEND
MOVE 15 Rune [27,10]
ENUN
STAL 30
TEXTCONT
TEXTEND
MOVE 15 Solveig [11,11]
STAL 30
MOVE 15 0x70 [10,10]
MOVE 15 0x71 [10,12]
ENUN
STAL 30
MUSC RippleIslandEnding
TEXTCONT
TEXTEND
REMA
SPAWN_ALLY Eimund [26,9]
ENUN
CheckMaxHP(Eimund)
SADD s1 sC s0
SET_HP Eimund
EVBIT_T 9
MOVE 15 Asgeir [25,10]
ENUN
STAL 30
EVBIT_F 5
Text(Ch3EndingScene3)
MNC2 0x03//return to title screen
NoFade
ENDA